!! Verb inflection
!  ---------------
!! The Tokelauan language verbs inflect for number.

!! # Prefixes

LEXICON Verbs !!= @CODE@ splits to subgrups according to pl form
2v_pref ;
3v_pref ;
tav_pref ;
ta_pref ;
3redup_pref ;
4redup_pref ;
aux ;
v_particles ;

!! * S = syll; R, E = C V in redup pattern

LEXICON 2v_pref !!= * @CODE@ = 2-syllabic verbs with CV reduplication
@U.verb.sg@                   2v_stems ; ! sg verbs
@U.verb.pl@:@U.verb.pl@^R^E%> 2v_stems ; ! pl verbs

LEXICON 3v_pref !!= * @CODE@ = 3-syllabic verbs with CV reduplication
@U.verb.sg@                 3v_stems ; ! sg verbs
@U.verb.pl@:@U.verb.pl@^3S  3v_stems ; ! pl verbs

LEXICON tav_pref !!= * @CODE@ = 3-syllabic verbs with CV to ta
@U.verb.sg@                 tav_stems ; ! sg verbs
@U.verb.pl@:@U.verb.pl@^T^A tav_stems ; ! pl verbs

LEXICON ta_pref !!= * @CODE@ = 2-syllabic verbs with ta prefix
@U.verb.sg@               ta_stems ; ! sg verbs
@U.verb.pl@:@U.verb.pl@ta ta_stems ; ! pl verbs

LEXICON 3redup_pref !!= * @CODE@ = 2-syllabic verbs with full reduplication
@U.verb.sg@                3redup_stems ; ! sg verbs
@U.verb.pl@:@U.verb.pl@^E1^R2^E2%> 3redup_stems ; ! pl verbs

LEXICON 4redup_pref !!= * @CODE@ = 2-syllabic verbs with full reduplication
@U.verb.sg@                4redup_stems ; ! sg verbs
@U.verb.pl@:@U.verb.pl@^R1^E1^R2^E2%> 4redup_stems ; ! pl verbs


!! # Suffixes

LEXICON vsg !!= * @CODE@ 
+V+Sg: # ; ! fano

LEXICON vpl !!= * @CODE@ 
+V+Pl: # ; !olo

LEXICON vtags !!= * @CODE@ = adds POS and Sg, Pl tags as governed by the U flags
@U.verb.sg@+V+Sg:@U.verb.sg@ # ;
@U.verb.pl@+V+Pl:@U.verb.pl@ # ;

LEXICON tvtags !!= * @CODE@ = adds POS and Sg, Pl tags as governed by the U flags
@U.verb.sg@+V+Sg:@U.verb.sg@ # ;
@U.verb.pl@+V+Pl:@U.verb.pl@ # ;
@U.verb.sg@+V+Sg:@U.verb.sg@ tv ;
@U.verb.pl@+V+Pl:@U.verb.pl@ tv ;

LEXICON tv !!= * @CODE@ 
# ;
+TV:%>a # ;
+Dir+TV:mai%>a # ;
+Dir+TV:atu%>a # ;
+Dir+TV:ifo%>a # ;
+Dir+TV:ake%>a # ;


LEXICON redup_suff !!= * @CODE@ = adds POS and Sg, Pl tags as governed by the U flags
+V+Sg: redup_suff_sgflags ;
+V+Pl: redup_suff_plflags ;

LEXICON redup_suff_sgflags !!= * @CODE@ 
@U.verb.sg@ # ;
LEXICON redup_suff_plflags !!= * @CODE@ 
@U.verb.pl@ # ;

! vim: set ft=xfst-lexc:

